load("@enkit//bazel/utils:binary.bzl", "declare_binary")
load("@rules_foreign_cc//foreign_cc:configure.bzl", "configure_make")

filegroup(name = "all", srcs = glob(["**"]), visibility = ["//visibility:public"])

configure_make(
    name = "binaries",
    lib_source = ":all",
    configure_options = [
        "--disable-lastlog",
        "--disable-utmp",
        "--disable-utmpx",
        "--disable-wtmp",
        "--disable-wtmpx",
        "--disable-loginfunc",
        "--disable-static",
        "--enable-bundled-libtom",
    ] + select({
        "@//bazel/platforms:e1_gnu_build": [
            "--host=aarch64-none-linux-gnu",
        ],
        "//conditions:default": [],
    }),
    # By default `targets` would be ["", "install"], meaning it would run
    # `make ""` and `make "install"`.
    #
    # Don't run "make install" as the dropbear makefile does things to
    # setup the privileges correctly that don't work in an RBE environment.
    # Instead, run the few commands in postfix_script to install the
    # binaries correctly.
    targets = [""],
    postfix_script = "; ".join([
        "mkdir -p ${INSTALLDIR}/bin",
        "cp dropbear dropbearkey ${INSTALLDIR}/bin",
    ]),
    out_binaries = [
        "dropbear",
        "dropbearkey",
    ],
    visibility = ["//visibility:public"],
    deps = select({
        "@//bazel/platforms:e1_gnu_build": [
            "@libz//:libz",
        ],
        # Use the system libz on other platforms.
        "//conditions:default": [],
    }),
)

declare_binary(
    name = "dropbear",
    path = "dropbear",
    dep = ":binaries",
)

declare_binary(
    name = "dropbearkey",
    path = "dropbearkey",
    dep = ":binaries",
)
